Prefix 7 "↓" := squash.
Infix 3 "∈" := member.
Infix 7 "≼" := approx.
Infix 4 "⇔" := iff.

Theorem squash_wf : [ {T:U{i}} ↓ T ∈ U{i} ] {
  unfold <squash>; auto
}.

Theorem implies_squash : [ {T:U{i}} T => ↓ T ] {
  *{intro}; aux {auto};
  @{ [H: 'T |- _] => witness [lam(_.<>) H]};
  unfold <squash>; auto
}.

Theorem approx_id_refl_wf : [
  =(lam(x.x) ≼ lam(x.x) ; <> ≼ <> ; U{i})
] {
  unfold <member>; ext;
  cut-lemma <implies_squash>;
  elim #1 [lam(x.x) ≼ lam(x.x) ⇔ <> ≼ <>];
  main {
    assert [lam(x.x) ≼ lam(x.x) ⇔ <> ≼ <>];
    main { elim #2 [H]; auto };
  };
  aux {unfold <iff and implies>; auto}
}.

Theorem test_image_elim : [
  {n:nat} {m:nat} {x : ↓ =(n; m; nat)} x ∈ ↓ =(n; m; nat) => =(n; m; nat)
] {
  unfold <squash member>;
  *{intro}; aux {auto};
  @{[H : image(_;_) |- _] => elim <H>};
  auto
}.
