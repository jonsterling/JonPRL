||| Try the first introduction rule and if we can't
||| immediately solve the goal backtrack and try the
||| next.
Tactic constructor {
  (!{intro #0; auto} | !{intro #1; auto})
}.

Tactic constructor2 {
  (!{intro #0; auto; constructor} | !{intro #1; auto; constructor})
}.

Tactic elims {
  *{ @{ [H : P + Q |- _] => elim <H>; thin <H>
      | [H : void |- _] => elim <H>
      | [H : P |- P] => hypothesis <H>
      }
   }
}.

Tactic auto-sums {
  auto; elims; constructor2
}.

Theorem commutative : [{A:U{i}} {B:U{i}} iff(A + B; B + A)] {
  auto-sums
}.

Theorem plus-assoc : [{A:U{i}} {B:U{i}} {C:U{i}} iff(A + (B + C); (A + B) + C)] {
  auto-sums
}.

Theorem void-idr : [{A:U{i}} iff(A + void; A)] {
  auto-sums
}.

Theorem void-idl : [{A:U{i}} iff(void + A; A)] {
  auto-sums
}.
