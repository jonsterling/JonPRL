Operator RawCat : ().
Operator obj : (0).
Operator hom : (0;0;0).
Operator idn : (0;0).
Operator cmp : (0;0;0;0;0;0).

[RawCat] =def= [
  (Obj:U{i})
  * (Hom : Obj -> Obj -> U{i})
  * (Idn : (A:Obj) Hom A A)
  * (Cmp : (A:Obj) (B:Obj) (C:Obj) Hom B C -> Hom A B -> Hom A C)
  * unit
].

[obj(C)] =def= [spread(C; x.y.x)].
[hom(C;A;B)] =def= [spread(spread(C; x.y.y); x.y.x) A B].
[idn(C;A)] =def= [spread(spread(spread(C; x.y.y); x.y.y); x.y.x) A].
[cmp(C;X;Y;Z;f;g)] =def= [spread(spread(spread(spread(C; x.y.y); x.y.y); x.y.y); x.y.x) X Y Z f g].

Tactic rawcat-unfold {
  unfold <RawCat obj hom idn cmp>.
}.

Tactic autoR {
  reduce; *{auto; reduce}.
}.

Theorem RawCat-wf : [member(RawCat; U{i'})] {
  rawcat-unfold; auto.
}.


Theorem obj-wf : [{RC:RawCat} member(obj(RC); U{i})] {
  rawcat-unfold; auto.
}.

Theorem hom-wf : [
  {RC:RawCat} {A:obj(RC)} {B:obj(RC)} member(hom(RC;A;B); U{i})
] {
  rawcat-unfold; autoR.
}.

Theorem idn-wf : [
  {RC:RawCat} {A:obj(RC)} member(idn(RC; A); hom(RC; A; A))
] {
  rawcat-unfold; autoR.
}.

Theorem cmp-wf : [
  {RC:RawCat}
  {X:obj(RC)}
  {Y:obj(RC)}
  {Z:obj(RC)}
  {f:hom(RC; Y; Z)}
  {g:hom(RC; X; Y)}
    member(cmp(RC;X;Y;Z;f;g); hom(RC; X;Z))
] {
  rawcat-unfold; autoR.
}.

Operator LeftIdentity : (0).
[LeftIdentity(C)] =def= [{A:obj(C)} {B:obj(C)} {f:hom(C;A;B)} =(cmp(C;A;B;B; idn(C;B); f); f; hom(C;A;B))].

Operator RightIdentity : (0).
[RightIdentity(C)] =def= [{A:obj(C)} {B:obj(C)} {f:hom(C;A;B)} =(cmp(C;A;A;B; f; idn(C;A)); f; hom(C;A;B))].

Operator CmpAssoc : (0).
[CmpAssoc(C)] =def= [
  {W:obj(C)} {X:obj(C)} {Y:obj(C)} {Z:obj(C)}
  {f:hom(C;W;X)}
  {g:hom(C;X;Y)}
  {h:hom(C;Y;Z)}
    =(cmp(C;W;X;Z; cmp(C;X;Y;Z; h;g); f);
      cmp(C;W;Y;Z; h; cmp(C;W;X;Y; g;f));
      hom(C;W;Z))
].

Tactic isect-RawCat {
  @{ [|- {RC : RawCat} P] => intro @i'; focus 1 #{ lemma <RawCat-wf> }
   }
}.

Tactic basic-wf {
  *{ @{ [|- =(obj(C); obj(C); _)] => cut-lemma <obj-wf>; unfold <member>; bhyp <obj-wf>
      | [|- =(hom(C;X;Y); hom(C;X;Y); _)] => cut-lemma <hom-wf>; unfold <member>; bhyp <hom-wf>
      | [|- =(cmp(C;X;Y;Z;f;g); cmp(C;X;Y;Z;f;g); _)] => cut-lemma <cmp-wf>; unfold <member>; bhyp <cmp-wf>
      }; auto
   }
}.

Theorem LeftIdentity-wf : [{RC:RawCat} member(LeftIdentity(RC); U{i})] {
  isect-RawCat;
  unfold <LeftIdentity>; auto;
  basic-wf; rawcat-unfold; autoR
}.

Theorem RightIdentity-wf : [{RC:RawCat} member(RightIdentity(RC); U{i})] {
  isect-RawCat;
  unfold <RightIdentity>; auto;
  basic-wf; rawcat-unfold; autoR
}.

Theorem CmpAssoc-wf : [{RC:RawCat} member(CmpAssoc(RC); U{i})] {
  unfold <CmpAssoc>; isect-RawCat; auto;
  basic-wf
}.

Operator LawCat : (0).
[LawCat(RC)] =def= [LeftIdentity(RC) * RightIdentity(RC) * CmpAssoc(RC)].

Tactic lawcat-unfold {
  unfold <LeftIdentity RightIdentity CmpAssoc LawCat>;
  rawcat-unfold.
}.

Tactic basic-wf' {
  basic-wf;
  *{ @{ [|- =(LeftIdentity(C); LeftIdentity(C); _)] => cut-lemma <LeftIdentity-wf>; unfold <member>; bhyp <LeftIdentity-wf>
      | [|- =(RightIdentity(C); RightIdentity(C); _)] => cut-lemma <RightIdentity-wf>; unfold <member>; bhyp <RightIdentity-wf>
      | [|- =(CmpAssoc(C); CmpAssoc(C); _)] => cut-lemma <CmpAssoc-wf>; unfold <member>; bhyp <CmpAssoc-wf>
      }; auto
   }
}.

Theorem LawCat-wf : [{RC:RawCat} member(LawCat(RC); U{i})] {
  unfold <LawCat>; isect-RawCat; auto;
  basic-wf'
}.

Operator Cat : ().
[Cat] =def= [{C : RawCat | LawCat(C)}].

Tactic cat-unfold {
  unfold <Cat>; lawcat-unfold.
}.

Theorem Cat-wf : [member(Cat; U{i'})] {
  unfold <Cat>; auto;
  focus 0 #{cut-lemma <RawCat-wf>; unfold <member>; auto};
  cut-lemma <LawCat-wf>; cum @i; unfold <member>;
  bhyp <LawCat-wf>; auto
}.

Theorem InitialRawCat : [RawCat] {
  unfold <RawCat>;
  intro [void]; auto;
  intro [lam(A. lam(B. void))]; auto
}.

Theorem InitialCat : [Cat] {
  unfold <Cat>;
  intro [InitialRawCat] @i;
  unfold <InitialRawCat>;
  [id, id, cut-lemma <LawCat-wf>; elim #2 [C]; auto];

  |||TODO: we need a rule that says "this is the extract of this lemma, and is thence well-formed"
  *{lawcat-unfold; auto; reduce}
}.

Theorem TerminalRawCat : [RawCat] {
  unfold <RawCat>;
  intro [unit]; autoR;
  intro [lam(A. lam(B. unit))]; autoR;
}.

Tactic unit-eta {
  @{ [f : unit |- =(f; <>; unit)] => elim <f>; eq-cd }
}.

Theorem TerminalCat : [Cat] {
  unfold <Cat>;
  intro [TerminalRawCat] <C> @i; unfold <TerminalRawCat>;
  focus 2 #{
    cut-lemma <LawCat-wf>; bhyp <LawCat-wf>; auto
  };

  *{lawcat-unfold; auto; reduce};
  symmetry; unit-eta
}.
