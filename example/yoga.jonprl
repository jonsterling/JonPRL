Tactic autoR {
  reduce; *{auto; reduce}
}.

Operator fst : (0).
Operator snd : (0).
Operator pairF : (0;0).
Operator pairFp : (0;0).

[fst(p)] =def= [spread(p; x.y.x)].
[snd(p)] =def= [spread(p; x.y.x)].
[pairF(f;g)] =def= [lam(x. pair(f x; g x))].
[pairFp(f;g)] =def= [lam(x. pairF(f fst(x); g snd(x)))].

Operator CTT : ().
Operator Paths : (0).

[CTT] =def= [lam(A.lam(B. A -> B))].
[Paths(A)] =def= [lam(x.lam(y. =(x;y;A)))].

Operator Yo : (0).
Operator oY : (0).

[Yo(B)] =def= [lam(A. A -> B)].
[oY(A)] =def= [lam(B. A -> B)].

Operator End : (0;0).
Operator Coend : (0;0).

[End(X;P)] =def= [(x:X) P x x].
[Coend(X;P)] =def= [(x:X) * P x x].

Operator Ran : (0;0;0;0;0).
Operator Lan : (0;0;0;0;0).

[Ran(X;cat;hom;g;h)] =def= [lam(a. End(X; lam(x.lam(y. hom (cat a (g x)) (h y)))))].
[Lan(X;cat;ten;g;h)] =def= [lam(a. Coend(X; lam(x.lam(y. ten (h x) (cat (g y) a)))))].

Operator Psh : (0).

[Psh(X)] =def= [X -> U{i}].

||| Grothendieck's Yoga of Six Operations

|||        ğ’ ---- f ---> ğ’Ÿ
||| ==============================
||| ğ’«(ğ’) --[ f â†“! âŠ£      ] -> ğ’«(ğ’Ÿ) â‰œ Lan (â‰¡) (âŠ—) f â‰… Î» Ï†. âˆ«â†‘ ğ“. Ï† ğ“ âŠ— (â‰¡) (f ğ“, -)
||| ğ’«(ğ’) <-[ f â†‘! â‰ƒ f â†‘* ] -- ğ’«(ğ’Ÿ) â‰œ         - âˆ˜ f â‰… Î» Ï†. Ï† âˆ˜ f
||| ğ’«(ğ’) --[      âŠ£ f â†“* ] -> ğ’«(ğ’Ÿ) â‰œ Ran (â‰¡) (â‹”) f â‰… Î» Ï†. âˆ«â†“ ğ“. (â‰¡) (-, f ğ“) â‹” Ï† ğ“

Operator Hom         : ().      ||| â‹”    âˆˆ U{i} â†’ U{i} â†’ U{iâ€²}
Operator Ten         : ().      ||| âŠ—    âˆˆ U{i} â†’ U{i} â†’ U{iâ€²}
Operator LowerShriek : (0;0;0). ||| f â†“! âˆˆ Psh(X) â†’ Psh(Y)
Operator UpperShriek : (0;0;0). ||| f â†‘! âˆˆ Psh(X) â† Psh(Y)
Operator UpperStar   : (0;0;0). ||| f â†‘* âˆˆ Psh(X) â† Psh(Y)
Operator LowerStar   : (0;0;0). ||| f â†“* âˆˆ Psh(X) â†’ Psh(Y)

[Hom] =def= [lam(A.lam(B. A -> B))].
[Ten] =def= [lam(A.lam(B. A * B))].
[LowerShriek(X;Y;f)] =def= [lam(Ï†.lam(x. Lan(X;Paths(Y);Ten;f;Ï†) x))].
[UpperShriek(X;Y;f)] =def= [lam(Ï†.lam(x. Ï† (f x)))].
[UpperStar(X;Y;f)] =def= [UpperShriek(X;Y;f)].
[LowerStar(X;Y;f)] =def= [lam(Ï†.lam(x. Ran(X;Paths(Y);Hom;f;Ï†) x))].

||| Proofs

Theorem End_wf : [{X:U{i}} {P : X -> X -> U{i}} member(End(X;P); U{i})] {
  unfold <End>; auto.
}.

Theorem Coend_wf : [{X:U{i}} {P : X -> X -> U{i}} member(Coend(X;P); U{i})] {
  unfold <Coend>; auto.
}.

Theorem Ran_wf : [
  {X:U{i}} {C:U{i}} {V:U{i}}
  {cat : C -> C -> U{i}}
  {hom : U{i} -> V -> U{i}}
  {g : X -> C}
  {h : X -> V}
  member(Ran(X;cat;hom;g;h); Psh(C))
] {
  unfold <Ran End Psh>; autoR.
}.

Theorem Lan_wf : [
  {X:U{i}} {C:U{i}} {V:U{i}}
  {cat : C -> C -> U{i}}
  {ten : V -> U{i} -> U{i}}
  {g : X -> C}
  {h : X -> V}
  member(Lan(X;cat;ten;g;h); Psh(C))
] {
  unfold <Lan Coend Psh>; autoR.
}.

Theorem Hom_wf : [{A:U{i}} {B:U{i}} member(Hom A B; U{i'})] {
  unfold <Hom>; reduce; auto.
}.

Theorem Ten_wf : [{A:U{i}} {B:U{i}} member(Ten A B; U{i'})] {
  unfold <Ten>; reduce; auto.
}.

Theorem LowerShriek_wf : [
  {X:U{i}} {Y:U{i}} {f : X -> Y}
  member(LowerShriek(X;Y;f); Psh(X) -> Psh(Y))
] {
  unfold <LowerShriek Lan Paths Ten Coend Psh>; autoR.
}.

Theorem UpperShriek_wf : [
  {X:U{i}} {Y:U{i}} {f : X -> Y}
  member(UpperShriek(X;Y;f); Psh(Y) -> Psh(X))
] {
 unfold <UpperShriek Psh>; autoR.
}.

Theorem UpperStar_wf : [
  {X:U{i}} {Y:U{i}} {f : X -> Y}
  member(UpperStar(X;Y;f); Psh(Y) -> Psh(X))
] {
 unfold <UpperStar UpperShriek Psh>; autoR.
}.

Theorem LowerStar_wf : [
  {X:U{i}} {Y:U{i}} {f : X -> Y}
  member(LowerStar(X;Y;f); Psh(X) -> Psh(Y))
] {
  unfold <LowerStar Ran Paths Hom End Psh>; autoR.
}.

||| Adjunctions

||| coproduct âŠ£ weakening
||| L-! âˆ˜ R-!
||| â‰ƒ f â†“! âˆ˜ f â†‘!
||| â‰ƒ Lan Paths âŠ— f âˆ˜ (- âˆ˜ f)
||| â‰ƒ Î»g a. âˆ«â†‘x. (gâˆ˜f) x âŠ— f x  = a
||| â‰ƒ Î»g a. Î£ x. (gâˆ˜f) x Ã— f x  = a
||| â‰ƒ Î»Ï† a. âˆƒ x. Ï†(x[Î³]) âˆ§ x[Î³] = a | Î³ ~ f

||| weakening âŠ¢ coproduct
||| R-! âˆ˜ L-!
||| â‰ƒ f â†‘! âˆ˜ f â†“!
||| â‰ƒ (- âˆ˜ f) âˆ˜ Lan Paths âŠ— f
||| â‰ƒ Î»g a. âˆ«â†‘x. g x  âŠ— f x  = f a
||| â‰ƒ Î»g a. Î£ x. g x  Ã— f x  = f a
||| â‰ƒ Î»Ï† a. âˆƒ x. Ï†(x) âˆ§ x[Î³] = a[Î³] | Î³ ~ f

||| weakening âŠ£ product
||| L-* âˆ˜ R-*
||| â‰ƒ f â†‘* âˆ˜ f â†“*
||| â‰ƒ (- âˆ˜ f) âˆ˜ Ran Paths â‹” f
||| â‰ƒ Î»g a. âˆ«â†“x. f a  = f x  â‹” g x
||| â‰ƒ Î»g a. Î  x. f a  = f x  â†’ g x
||| â‰ƒ Î»Ï† a. âˆ€ x. a[Î³] = x[Î³] âŠƒ Ï†(x) | Î³ ~ f

||| product âŠ¢ weakening
||| R-* âˆ˜ L-*
||| â‰ƒ f â†“* âˆ˜ f â†‘*
||| â‰ƒ Ran Paths â‹” f âˆ˜ (- âˆ˜ f)
||| â‰ƒ Î»g a. âˆ«â†“x. a = f x  â‹” (gâˆ˜f) x
||| â‰ƒ Î»g a. Î  x. a = f x  â†’ (gâˆ˜f) x
||| â‰ƒ Î»Ï† a. âˆ€ x. a = x[Î³] âŠƒ Ï†(x[Î³]) | Î³ ~ f

||| contraction âŠ£ equality
||| L-Î´ âˆ˜ R-Î¸

||| equality âŠ¢ contraction
||| R-Î¸ âˆ˜ L-Î´

||| truth âŠ£ comprehension

||| comprehension âŠ¢ truth
